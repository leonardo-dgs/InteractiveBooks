import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = 'net.leonardo_dgs'
version = '1.4.8'
description = 'Create cool interactive books!'

String mainPackage = project.group + '.' + project.name.toLowerCase()
String apiVersion = '1.13'
String author = 'Leonardo_DGS'
String website = 'https://www.spigotmc.org/resources/45604/'

String spigotVersion = '1.15.2-R0.1-SNAPSHOT'
String bungeeChatVersion = '1.15-SNAPSHOT'
String lombokVersion = '1.18.12'
String helperVersion = '5.6.2'
String bStatsVersion = '1.5'
String papiVersion = '2.10.6'
String nbtApiVersion = '2.3.1'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://www.jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:' + spigotVersion
    compileOnly 'net.md-5:bungeecord-chat:' + bungeeChatVersion
    compileOnly 'io.netty:netty-all:4.1.42.Final'
    compileOnly 'me.clip:placeholderapi:' + papiVersion
    compileOnly 'org.projectlombok:lombok:' + lombokVersion
    annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
    implementation 'de.tr7zw:item-nbt-api:' + nbtApiVersion
    implementation 'org.bstats:bstats-bukkit-lite:' + bStatsVersion
    implementation 'me.lucko:helper:' + helperVersion
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "name"       : project.name,
            "mainPackage": mainPackage,
            "version"    : project.version,
            "apiVersion" : apiVersion,
            "description": project.description,
            "author"     : author,
            "website"    : website
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = project.name + '.jar'
    minimize()
    relocate 'de.tr7zw.changeme.nbtapi', mainPackage + '.nbtapi'
    relocate 'me.lucko.helper', mainPackage + ".helper"
    relocate 'org.bstats.bukkit', mainPackage + '.bstats'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}